plugins {
    // Basic Plugins
    id 'java-library'
    id 'groovy'
    id 'jacoco'

    id 'nebula.project' version '3.4.0'

    // Versioning Plugins
    id 'nebula.release' version '6.0.2'

    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.jfrog.bintray' version '1.7.3'
}

group = 'org.slog4j'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
}

dependencies {
    compileOnly(
        [group: 'org.projectlombok', name: 'lombok', version: '1.16.18'],
    )

    api(
        [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'],
        [group: 'org.joda', name: 'joda-convert', version: '1.9.2'],
    )

    implementation(
        [group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'],
    )

    testCompile(
        [group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.12'],
        [group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'],
        [group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'],
    )
}

tasks.withType(Test) {
    jvmArgs '-Duser.timezone=America/Sao_Paulo'

    // show standard out and standard error of the test JVM(s) on the console
    testLogging {
        events 'started', 'passed', 'skipped', 'failed'
        showStandardStreams = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'slog4j'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/eliezio/slog4j.git'
        githubRepo = 'eliezio/slog4j'
        githubReleaseNotesFile = 'README.md'
        version {
            name = project.version
            desc = "SLog4j $project.version final"
            vcsTag = "v$project.version"
        }
    }
}

// Remove automatic dependency set by nebula-release when it detects org.jfrog.bintray plugin
// see https://github.com/nebula-plugins/nebula-release-plugin/blob/master/src/main/groovy/nebula/plugin/release/ReleasePlugin.groovy#L250
postRelease.dependsOn = postRelease.taskDependencies.values - bintrayUpload
