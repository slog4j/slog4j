plugins {
    // Basic Plugins
    id 'java-library'
    id 'groovy'
    id 'jacoco'
    id 'maven-publish'

    // Versioning Plugins
    id 'nebula.release' version '6.0.2'

    id 'com.adarshr.test-logger' version '1.1.2'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.jfrog.bintray' version '1.8.0'
}

group = 'org.slog4j'
description = 'Structured Event Logging for Java'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
}

dependencies {
    compileOnly(
        [group: 'org.projectlombok', name: 'lombok', version: '1.16.18'],
    )

    api(
        [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'],
        [group: 'org.joda', name: 'joda-convert', version: '1.9.2'],
    )

    implementation(
        // 3.5 is the latest version that supports Java 6.
        [group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'],
        [group: 'org.yaml', name: 'snakeyaml', version: '1.19'],
    )

    testCompile(
        [group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'],
    )

    testRuntime(
        [group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.11'],
    )
}

testlogger {
    theme 'mocha'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name 'MIT License'
            url 'https://opensource.org/licenses/MIT'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'eliezio'
            name 'Eliezio Oliveira'
            email 'ebo@pobox.com'
        }
    }

    scm {
        url 'https://github.com/eliezio/slog4j.git'
    }
}

publishing {
    publications {
        slog4j(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
            groupId group
            artifactId name
            version version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Structured Event Logging for Java')
                root.appendNode('name', project.name)
                root.appendNode('url', 'https://github.com/eliezio/slog4j.git')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['slog4j']
    pkg {
        repo = 'maven'
        name = 'slog4j'
        userOrg = user
        desc = 'Structured Event Logging for Java'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/eliezio/slog4j.git'
        publicDownloadNumbers = true
        githubRepo = 'eliezio/slog4j'
        githubReleaseNotesFile = 'README.adoc'
        version {
            name = project.version
            vcsTag = "v$project.version"
        }
    }
}
